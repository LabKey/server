import org.labkey.gradle.task.ShowDiscrepancies
import org.labkey.gradle.util.BuildUtils
import org.labkey.gradle.util.GroupNames

buildscript {
    repositories {
        jcenter()
        maven {
            url "${artifactory_contextUrl}/libs-release"
        }
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        maven {
            url "${artifactory_contextUrl}/plugins-snapshot-local"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:${buildInfoExtractorGradleVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayPluginVersion}"
        classpath "com.moowork.gradle:gradle-node-plugin:${gradleNodePluginVersion}"
        classpath "org.labkey.build:versioning:${versioningPluginVersion}"
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
//        classpath 'org.owasp:dependency-check-gradle:3.0.2'
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

}

allprojects {
    // Comment in to enable the tasks for owasp dependency checking
//    apply plugin: 'org.owasp.dependencycheck'
//    dependencyCheck {
//        outputDirectory = "${project.rootProject.buildDir}/reports/dependencyCheck/${project.path.replaceAll(':','_').substring(1)}"
//    }
    if (BuildUtils.shouldPublish(project) || BuildUtils.shouldPublishDistribution(project))
    {
        apply plugin: 'com.jfrog.artifactory'
    }
    project.tasks.withType(JavaCompile) {
        JavaCompile compile ->
            compile.options.incremental = true  // Gradle 3.4
            compile.options.encoding = 'UTF-8'
    }
}

// We need the tomcat home directory to find certain dependencies for our build process.
String tomcatDirProp = project.hasProperty('teamcity') ? teamcity['tomcat.home'] : (System.getenv('CATALINA_HOME') != null ? System.getenv('CATALINA_HOME') : System.properties['tomcat.home'])

if (tomcatDirProp == null)
    throw new GradleException("Tomcat home directory not set.  Please set the value of the system property tomcat.home " +
            "using either systemProp.tomcat.home=<tomcat home directory> in a gradle.properties file " +
            "or -Dtomcat.home=<tomcat home directory> on command line. Setting the environment variable CATALINA_HOME " +
            "will also work when building just from the command line (i.e., not in IntelliJ).")
def tomcatHome = new File(tomcatDirProp)
if (!tomcatHome.exists())
    throw new GradleException("Tomcat home directory ${tomcatDirProp} does not exist.")

//allprojects {
//    apply plugin: 'idea'
//    idea {
//        module {
//            outputDir new File(rootDir, "intellijBuild/${project.name}/classes/main")
//            testOutputDir new File(rootDir, "intellijBuild/${project.name}/classes/test")
//        }
//    }
//}

allprojects {
    configurations {
        driver
    }

    dependencies {
        //for SqlUtils
        driver "net.sourceforge.jtds:jtds:${jtdsVersion}"
        driver "org.postgresql:postgresql:${postgresqlDriverVersion}"
    }

    if (project.hasProperty('includeVcs'))
    {
        apply plugin: 'org.labkey.versioning'
        versioning {
            user = project.hasProperty('svn_user') ? project.property('svn_user') : 'cpas'
            password = project.hasProperty('svn_password') ? project.property('svn_password') : 'cpas'
        }
    }

    apply plugin: 'org.labkey.base'
    group  'org.labkey'
    version BuildUtils.getVersionNumber(project)

    tasks.withType(JavaCompile) { 
        sourceCompatibility = project.ext.sourceCompatibility
        targetCompatibility = project.ext.targetCompatibility
    }

    ext
            {
                tomcatDir = tomcatDirProp
                tomcatConfDir = "${tomcatDir}/conf/Catalina/localhost"
                vcsRevision = BuildUtils.getStandardVCSProperties(project).getProperty("VcsRevision")
                installerVersion = "${project.version}-${vcsRevision}"
            }
    repositories
            {
                // this if statement is necessary because the TeamCity artifactory plugin overrides
                // the repositories but does not use these artifactory_ urls.  For others who are
                // developing or building, you do need to specify the three artifactory_ properties
                // used below.
                if (project.hasProperty("artifactory_contextUrl"))
                {
                    maven {

                        url "${artifactory_contextUrl}/libs-release"

                        if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password'))
                        {
                            credentials {
                                username = artifactory_user
                                password = artifactory_password
                            }
                            authentication {
                                basic(BasicAuthentication) // enable preemptive authentication to get around https://www.jfrog.com/jira/browse/RTFACT-4434
                            }
                        }
                    }
                    maven {
                        url "${artifactory_contextUrl}/libs-snapshot"

                        if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password'))
                        {
                            credentials {
                                username = artifactory_user
                                password = artifactory_password
                            }
                            authentication {
                                basic(BasicAuthentication) // enable preemptive authentication to get around https://www.jfrog.com/jira/browse/RTFACT-4434
                            }
                        }
                    }
                }
                flatDir dirs: "${tomcatDir}/lib"
                flatDir dirs: "${tomcatDir}/bin"  // for tomcat-juli.jar (required with jsp compile) and bootstrap.jar
                jcenter()
            }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    configurations.all
            {Configuration config ->
                resolutionStrategy {
                    // we force this version because we have compilation problems with version 1.9.2 of commons-beanutils, which is the version
                    // that is brought in by Gradle's conflict resolution as a result of our chosen versions for commons-validator (1.5.0) and commons-digester (1.8.1)
                    force "commons-beanutils:commons-beanutils:${commonsBeanutilsVersion}"
                    // we get different versions of slf4j-log4j12 and slf4j-api from different transitive dependencies.
                    force "org.slf4j:slf4j-log4j12:${slf4jLog4j12Version}"
                    force "org.slf4j:slf4j-api:${slf4jLog4jApiVersion}"
                    // force some newer versions than are brought in by mondrian (et al.)
                    force "xerces:xercesImpl:${xercesImplVersion}"
                    force "log4j:log4j:${log4jVersion}"
                    // force version for consistency with saml, query, LDK, and pipeline
                    force "commons-lang:commons-lang:${commonsLangVersion}"
                    // force version for consistency with workflow, api, SequenceAnalysis
                    force "org.apache.commons:commons-lang3:${commonsLang3Version}"
                    force "commons-dbcp:commons-dbcp:${commonsDbcpVersion}"
                    force "commons-io:commons-io:${commonsIoVersion}"
                    force "commons-logging:commons-logging:${commonsLoggingVersion}"
                    force "commons-pool:commons-pool:${commonsPoolVersion}"
                    // force version for consistency with query, saml, LDK, api
                    force "commons-collections:commons-collections:${commonsCollectionsVersion}"
                    // force version for ms2, saml, fileTransfer, harvest, scrumtime, api, accounts, docker
                    force "commons-codec:commons-codec:${commonsCodecVersion}"
                    // force version for api, accounts, fileTransfer, harvest, scrumtime, docker
                    force "org.apache.httpcomponents:httpcore:${httpcoreVersion}"
                    // force version for api, docker, fileTransfer, harvest, scrumtime, accounts
                    force "org.apache.httpcomponents:httpclient:${httpclientVersion}"
                    // force version for SequenceAnalysis, api, cloud
                    force "com.google.guava:guava:${guavaVersion}"
                    // force version for accounts, api, query
                    force "javax.validation:validation-api:${validationApiVersion}"
                    // force version for accounts, docker, api, workflow
                    force "com.fasterxml.jackson.core:jackson-annotations:${jacksonAnnotationsVersion}"
                    // saml and query bring in different versions transitively; we force the later one
                    force "xalan:xalan:${xalanVersion}"
                    // genotyping brings in a much older version of this, so we force a newer version for compatibility
                    force "org.apache.commons:commons-compress:${commonsCompressVersion}"
                    // workflow (Activiti) and galaxyintegration bring in different versions of javax.activation:activation transitively, so
                    // we force the later version
                    force "javax.activation:activation:${activationVersion}"
                    // force version for api, LDK, pipeline, query, saml, but not for the xsdDoc configuration, which requires
                    // an older version for the docflex library we use
                    if (!config.name.equals('xsdDoc'))
                        force "xml-apis:xml-apis:${xmlApisVersion}"
                    // cloud and SequenceAnalysis bring this in as a transitive dependency.  We resolve to the later version here to keep things consistent
                    force "com.google.code.gson:gson:${gsonVersion}"
                    // workflow (Activiti) brings in older versions of these libraries, so we need to force these versions
                    force "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
                    force "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
                    // The version of picard we depend on brings in an older version of htsjdk, but SequenceAnalysis depends on a later version
                    force "com.github.samtools:htsjdk:${htsjdkVersion}"
                    // Cloud module brings in earlier versions of this library, so we force the later one
                    force "org.apache.tika:tika-core:${tikaVersion}"
                    // Cloud module and workflow module bring in different versions
                    force "org.slf4j:jcl-over-slf4j:${jclOverSlf4jVersion}"
                    dependencySubstitution {
                        // Because the client api artifact name is not the same as the directory structure, we use
                        // Gradle's dependency substitution so the dependency will appear correctly in the pom files that
                        // are generated.  Because dependency substitution does not understand the use of classifiers, we cannot
                        // use this mechanism in general (plus, it's very slow for our many-module build).
                        if (BuildUtils.shouldBuildFromSource(project.project(":remoteapi:java")))
                            substitute module('org.labkey:labkey-client-api') with project(":remoteapi:java")
                        // mule and tika bring in different versions of bouncycastle package via transitive dependencies, but these versions
                        // result in a StackOverflow when starting tomcat so we substitute new libraries for both versions that are brought in.
                        substitute module('bouncycastle:bcprov-jdk14:138') with module("org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}")
                        substitute module('bouncycastle:bcmail-jdk14:138') with module("org.bouncycastle:bcpkix-jdk15on:${bouncycastleVersion}")
                        substitute module('bouncycastle:bcprov-jdk15:1.45') with module("org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}")
                        substitute module('bouncycastle:bcmail-jdk15:138') with module("org.bouncycastle:bcpkix-jdk15on:${bouncycastleVersion}")

                        // Cloud module (jclouds->guice->cglib) wants asm-3.1.jar but search module (tika) wants asm-debug-all-5.0.4.jar.. so force to more recent, debug version
//                        substitute module('asm:asm') with module("org.ow2.asm:asm-debug-all:${asmVersion}")
                    }
                }
            }
}


if (BuildUtils.shouldPublish(project) || BuildUtils.shouldPublishDistribution(project))
{
    artifactory {
        contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
        publish {
            repository {
                repoKey = BuildUtils.getRepositoryKey(project)
                if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password'))
                {
                    username = artifactory_user
                    password = artifactory_password
                }
                maven = true
            }
            defaults
                    {
                        publishPom = true
                        publishIvy = false
                    }
        }
    }

    artifactoryPublish.skip = true
}

subprojects {
    task("showRepos", group: "Help", description: "Show the list of repositories currently in use.").doLast({
        repositories.each{
            println "repository: ${it.name} (${it.hasProperty("url") ? it.url : it.getDirs()})"
        }
    })
    task("allDepInsight",
            type: DependencyInsightReportTask,
            group: "Help",
            description: "Displays the insight into a specific dependency across all projects")
    task("allDependencies",
            type: DependencyReportTask,
            group: "Help",
            description: "Displays the dependencies for all projects")
}

// We copy the resource files to a separate directory so they can be copied to jsptmp directory
// for use in compiling JSPs.  Copying directly from the project.sourceSets.webapp.output for the JSP
// task for api does not work when modules may be evaluated before the api module is evaluated.  Attempts to
// declare evaluationDependsOn(:server:api) for each of these projects did not work.
// This task is defined at the root to eliminate problems with evaluation order
project.task("copyTagLibsBase",
        type: Copy,
        {CopySpec copy ->
            copy.from "server/api/webapp"
            copy.into "${project.buildDir}/webapp"
            copy.include 'WEB-INF/web.xml'
            copy.include 'WEB-INF/*.tld'
            copy.include 'WEB-INF/tags/**'
        }
)

project.task("showDiscrepancies",
                group: "Help",
                type: ShowDiscrepancies,
                description: "Report external dependencies that have more than one version referenced in the modules in this build"
)

project.task(
        'getModulesManifest',
        group: GroupNames.DISTRIBUTION,
        description: "Creates a csv file with a list of the modules for each distribution. By default it will be " +
                "placed in a directory called manifests in the root of this labkey installation. Specify another " +
                "directory by using \"-PmanifestDir\". The default file name is distributionModules.csv; specify " +
                "another name using \"-PmanifestFileName\"."
).doLast({
    String manifestDir = "${project.getRootProject().getProjectDir()}/manifests"
    String manifestFileName = "distributionModules.csv"

    if (project.hasProperty("manifestDir")) {
        manifestDir = project.manifestDir
    }

    if (project.hasProperty("manifestFileName")) {
        manifestFileName = project.manifestFileName
    }

    project.mkdir(manifestDir)
    File manifestFile = project.file("${manifestDir}/${manifestFileName}")

    StringBuilder manifestString = new StringBuilder("distribution,moduleName\n")
    project.allprojects({Project sp ->
        if (sp.configurations.getNames().contains("distribution")) {
            String distName
            if (sp.getName().startsWith("test_")) {
                // This is for the test_*prc case.
                distName = sp.getName()
            }
            else {
                distName = sp.tasks.distribution.hasProperty("subDirName") ?
                        sp.tasks.distribution.subDirName :
                        sp.getName()
            }
            sp.configurations.distribution
                    .getAllDependencies()
                    .withType(ProjectDependency)
                    .each({dep ->
                manifestString.append("${distName},${dep.name}\n")
            })
        }
    })
    manifestFile.text = manifestString.toString()
})
