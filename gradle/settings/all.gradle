buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }

        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}
import org.labkey.gradle.util.BuildUtils

/*
  This file is used to determine which projects will be configured during the Gradle build of LabKey Server. 

  You need only provide the leaves of the project tree here; a Gradle project will be created for each 
  node in the tree with these leaves (e.g., 'server:modules:core' will create projects for ":server"
  ":server:modules" and ":server:modules:core").
 */
List<String> apiModules = ['labkey-api-js', 'labkey-api-r', 'labkey-api-python'] // Might be enlisted in server/optionalModules
List<String> excludedExternalModuleDirs = ["maccoss"] // Should be lower-case
List<String> excludedExternalModules = ["test"] // 'externalModules/scharp/test' is not a module

if (hasProperty('excludeExternalModuleDirs'))
{
    excludedExternalModuleDirs.addAll("${excludeExternalModuleDirs}".toLowerCase().split(","))
}

// This line below inclues all modules in the server/modules, server/customModules and server/optionalModules directories
BuildUtils.includeModules(this.settings, rootDir, BuildUtils.SERVER_MODULE_DIRS, apiModules);

List<String> externalModuleDirs = new ArrayList<>();
for (File f : new File(rootDir, "externalModules").listFiles())
{
    if (f.isDirectory() && !excludedExternalModuleDirs.contains(f.getName().toLowerCase()))
    {
        externalModuleDirs.add("externalModules/" + f.getName());
    }
}
// The line below includes all modules in the externalModules directory (except the ones indicates as to be excluded)
BuildUtils.includeModules(this.settings, rootDir, externalModuleDirs, excludedExternalModules)

// include the test distribution, which is used to create an artifact for TeamCity to pass around to the agents
include ":server:test:distributions:teamcity"

// include the jdbc api for testing on TeamCity.
if (new File(getRootDir(), "remoteapi/labkey-api-jdbc").exists())
{
    include ":remoteapi:labkey-api-jdbc"
}
