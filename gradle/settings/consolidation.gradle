// This module set builds all modules in the following directories:
// 
//    /server/customModules
//    /server/modules
//    /server/optionalModules
//    /server/test/modules
//    
// Goal is to build all the LabKey modules, i.e., all modules except for externalModules. This module set can be useful when
// refactoring core code, performing the end-of-release-cycle script consolidation process, updating licenses, etc.
//
// A few external modules are included since the EHR modules depend on them to build and/or run.

buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }

        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}
import org.labkey.gradle.util.BuildUtils

List<String> excludedModules = [
    'labkey-api-js', 'labkey-api-r', 'labkey-api-python'     // Might be enlisted in server/optionalModules
]

include ":externalModules:labModules:LDK"                // EHR modules depend on LDK
include ":externalModules:labModules:laboratory"         // Without this, EHR builds fail with "Could not find LDK-api.jar" error
include ":externalModules:onprcEHRModules:mergesync"     // onprc_ehr runtime dependency

// This line includes all modules in the server/modules, server/customModules and server/optionalModules directories
BuildUtils.includeModules(this.settings, rootDir, BuildUtils.SERVER_MODULE_DIRS, excludedModules);
