// This module set builds all modules in the following directories:
// 
//    /server/customModules
//    /server/modules
//    /server/optionalModules
//    /server/test/modules
//    
// Goal is to build all the LabKey modules, skipping most of the modules in externalModules. This module set can be useful when
// refactoring core code, performing the end-of-release-cycle script consolidation process, updating licenses, etc.
//
// Some external modules are built: the snprc EHR modules plus EHR build-time and runtime dependencies.

buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }

        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}
import org.labkey.gradle.util.BuildUtils

List<String> excludedModules = [
    'labkey-api-js', 'labkey-api-r', 'labkey-api-python'     // Might be enlisted in server/optionalModules
]

include ":server:modules:LabDevKitModules:LDK"                // EHR modules depend on LDK
include ":server:modules:LabDevKitModules:laboratory"         // Without this, EHR builds fail with "Could not find LDK-api.jar" error
include ":externalModules:onprcEHRModules:mergesync"     // onprc_ehr runtime dependency

include ":externalModules:snprcEHRModules:snprc_ehr"
include ":externalModules:snprcEHRModules:snprc_genetics"
include ":externalModules:snprcEHRModules:snprc_r24"
include ":externalModules:snprcEHRModules:snprc_scheduler"

// This line includes all modules in the server/modules, server/customModules and server/optionalModules directories
BuildUtils.includeModules(this.settings, rootDir, BuildUtils.SERVER_MODULE_DIRS, excludedModules);
