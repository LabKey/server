buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal {
            content {
                excludeGroupByRegex "org\\.labkey.*"
            }
        }
        maven {
            url "${artifactory_contextUrl}/plugins-release-no-proxy"
            mavenContent {
                releasesOnly()
            }
//            content {
//                includeGroup "org.labkey.build"
//                includeGroup "org.labkey.versioning"
//            }
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            mavenLocal()
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
                mavenContent {
                    snapshotsOnly()
                }
//                content {
//                    includeGroup "org.labkey.build"
//                    includeGroup "org.labkey.versioning"
//                }
            }

        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.configureEach {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}
import org.labkey.gradle.util.BuildUtils

/*
  This file specifies all of the modules to be included in the test distribution for the EHR TeamCity
  configuration.
 */

List<String> ehrModulesDirs = [
        "server/modules/johnsHopkinsEHRModules",
        "server/modules/nircEHRModules",
        "server/modules/onprcEHRModules",
        "server/modules/ehrModules",
        "server/modules/snprcEHRModules",
        "server/modules/wnprc-modules",
        "server/modules/LabDevKitModules"
]

BuildUtils.includeModules(this.settings, rootDir, [BuildUtils.PLATFORM_MODULES_DIR, BuildUtils.COMMON_ASSAYS_MODULES_DIR], [])
BuildUtils.includeModules(this.settings, rootDir, ehrModulesDirs, [])

include ":server:modules:snd"
include ":server:modules:tnprc_ehr"
include ":server:modules:tnprc_billing"
include ":server:modules:dataintegration"
include ":server:modules:premium"
include ":server:modules:ldap"

// include the test distribution, which is used to create an artifact for TeamCity to pass around to the agents
include "${BuildUtils.getTestProjectPath(this.settings.gradle)}:distributions:teamcity"
