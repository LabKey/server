import org.labkey.gradle.util.BuildUtils
import org.labkey.gradle.util.PomFileHelper

plugins {
    id 'java'
    id 'maven-publish'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

dependencies
        {
            def tomcatVersion = project.apacheTomcatVersion

            implementation "org.apache.tomcat:tomcat-api:${tomcatVersion}"
            implementation "org.apache.tomcat:tomcat-catalina:${tomcatVersion}"
            implementation "org.apache.tomcat:tomcat-juli:${tomcatVersion}"
            implementation "org.apache.tomcat:tomcat-util:${tomcatVersion}"
        }

def JAR_BASE_NAME = "labkeyBootstrap"
project.jar {
    archiveBaseName.set(JAR_BASE_NAME)
}
project.processResources.enabled = false
project.jar.manifest {
    attributes provider: 'LabKey'
    attributes 'Main-Class': "org.labkey.bootstrap.ModuleExtractor"
}

project.afterEvaluate  {
    project.publishing {
        publications {
            libs(MavenPublication) {
                groupId = project.group
                from components.java
                artifactId  = JAR_BASE_NAME
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = "LabKey Server Bootstrap Classes"
                    description = "The classes used when bootstrapping a LabKey Server instance"
                    developers PomFileHelper.getLabKeyTeamDevelopers()
                    licenses PomFileHelper.getApacheLicense()
                    organization PomFileHelper.getLabKeyOrganization()
                }
            }

            if (BuildUtils.shouldPublish(project))
            {
                project.artifactoryPublish {
                    dependsOn project.tasks.jar
                    publications('libs')
                }
            }

        }
    }
}


