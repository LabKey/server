import org.labkey.gradle.util.BuildUtils
import org.labkey.gradle.util.PomFileHelper

plugins {
    id "java"
    id "maven-publish"
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

// 'dependency-management' plugin doesn't allow us to use 'resolutionStrategy.force' to lock versions
ext['log4j2.version'] = log4j2Version

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    embedded
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"

    // This is a transitive dependency from spring-boot-starter that we're forcing to pick up CVE hotfixes. We're not
    // vulnerable since we're not accepting untrusted Spring Boot config files, but this cleans up the reporting.
    // At some point Spring Boot should update its preferred version and we can yank this
    implementation "org.yaml:snakeyaml:1.33"

    runtimeOnly "org.apache.tomcat.embed:tomcat-embed-jasper:${springBootTomcatVersion}"
    runtimeOnly "javax.mail:javax.mail-api:1.6.2"
    runtimeOnly group: "com.sun.mail", name: "javax.mail", version: "1.6.0"
    runtimeOnly group: "org.apache.tomcat", name: "tomcat-dbcp", version: "${springBootTomcatVersion}"
    runtimeOnly "org.postgresql:postgresql:${postgresqlDriverVersion}"

    developmentOnly("org.springframework.boot:spring-boot-devtools")
}

BuildUtils.addLabKeyDependency(project: project, config: "implementation", depProjectPath: BuildUtils.getBootstrapProjectPath(gradle))

project.publishing {
    publications {
        embeddedJar(MavenPublication) {
            groupId = 'org.labkey.build'
            artifactId = 'embedded'
            version = project.version
            artifact project.tasks.bootJar.outputs.files.singleFile
            pom {
                name = "LabKey Server Embedded"
                description = "LabKey classes for producing distributions with embedded TomCat."
                developers PomFileHelper.getLabKeyTeamDevelopers()
                licenses PomFileHelper.getApacheLicense()
                organization PomFileHelper.getLabKeyOrganization()
                scm PomFileHelper.getLabKeyGitScm()
            }
        }

        if (BuildUtils.shouldPublish(project))
        {
            project.artifactoryPublish {
                publications('embeddedJar')
            }
            project.artifactoryPublish.skip = false
        }
    }
}

project.artifacts {
    embedded project.tasks.bootJar
}

// The primary artifact from this build should be the jar produced from the bootJar task as that contains
// the SpringBoot classes.  This default jar task produces a jar file containing only the classes from src,
// which no one really needs.  If produced, this is the jar file that will be used in distributions, making
// them not really embedding anything.
jar {
 onlyIf { false }
}
